buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

ext.configFile = file('build.properties')
ext.config = parseConfig(configFile)

version = "${config.version}-build${config.build_number}"
group= "necro.convenientfoundation"

repositories {
    maven {
        name = "tterrag maven"
        url = "http://maven.tterrag.com/"
    }
    maven {
        name = "CoFH Maven"
        url = "http://maven.covers1624.net"
    }
    maven { url = "http://dvs1.progwml6.com/files/maven" }
    maven { url = "http://tehnut.info/maven" }
    maven { url = "http://dogforce-games.com/maven" }
}

dependencies {
    compile files(
            "libs/Baubles-${config.baubles_version}-deobf.jar"
    )
    compile "mezz.jei:jei_${config.jei_version}:api"
    runtime "mezz.jei:jei_${config.jei_version}"
    compile "mcjty.theoneprobe:TheOneProbe-1.12:${config.oneprobe_version}:api"
    runtime "mcjty.theoneprobe:TheOneProbe-1.12:${config.oneprobe_version}"
    deobfCompile "mcp.mobius.waila:Hwyla:${config.hwyla_version}"
    deobfCompile "gigaherz.guidebook:Guidebook-${config.guidebook_version}"
}

minecraft {
    version = "${config.mc_version}-${config.forge_version}"
    runDir = "run"

    mappings = config.mc_mappings
    replace 'GRADLE:BUILD', config.build_number
    replace 'GRADLE:VERSION', config.version

    //replaceIn 'ModConstants.java'
}

processResources {
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': config.mc_version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

def parseConfig(File cfg) {
    cfg.withReader {
        def prop = new Properties()
        prop.load(it)
        return (new ConfigSlurper().parse(prop))
    }
}

idea { module { inheritOutputDirs = true } }
