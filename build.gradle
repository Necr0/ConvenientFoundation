buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.0.1"
    }
}

repositories {
    maven {
        name = "tterrag maven"
        url = "http://maven.tterrag.com/"
    }
    maven {
        name = "CoFH Maven"
        url = "http://maven.covers1624.net"
    }
    maven { url = "http://dvs1.progwml6.com/files/maven" }
    maven { url = "http://dogforce-games.com/maven" }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.

compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

version = "${project.version}-${project.build_number}"
group= "necro.convenientfoundation"
archivesBaseName = "ConvenientFoundation-${project.mc_version}"

dependencies {
    compile files(
            "mod_libs/Baubles-${project.baubles_version}-api.jar"
    )
    compile "mezz.jei:jei_${project.jei_version}:api"
    runtime "mezz.jei:jei_${project.jei_version}"
    /*compile "mcjty.theoneprobe:TheOneProbe-1.12:${project.oneprobe_version}:api"
    runtime "mcjty.theoneprobe:TheOneProbe-1.12:${project.oneprobe_version}"
    deobfCompile "gigaherz.guidebook:Guidebook-${project.guidebook_version}"*/
}

minecraft {
    version = "${project.mc_version}-${project.forge_version}"
    runDir = "run"

    mappings = project.mc_mappings

    replace 'GRADLE:BUILD', project.build_number
    replace 'GRADLE:VERSION', project.version
    replaceIn 'LibMod.java'
}

processResources {
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.mc_version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

artifacts {
    archives sourceJar
}

publishing {
    tasks.publish.dependsOn 'build'
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar
        }
    }
}

Properties credentials = new Properties()
InputStream stream = new FileInputStream("$project.rootDir/credentials.properties")
credentials.load(stream)
stream.close()

artifactory {
    contextUrl = 'http://h2743441.stratoserver.net:8081/artifactory'
    publish {
        repository {
            repoKey = 'maven'

            username = credentials.getProperty("username")
            password = credentials.getProperty("password")
        }
        defaults {
            publications('mavenJava')
            publishArtifacts = true

            publishPom = true
        }
    }
}